buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.3'
        classpath 'io.fabric.tools:gradle:1.22.0'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.13.0'
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'checkstyle'
apply plugin: 'com.neenbedankt.android-apt'

check.dependsOn 'checkstyle'

task checkstyle(type: Checkstyle) {
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    classpath = files()
}

repositories {
    mavenCentral()
    mavenLocal()
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    compile project(':nfc-felica-lib')

    compile 'com.android.support:support-v4:25.1.0'
    compile 'com.google.android.gms:play-services-maps:10.0.1'
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.ryanharter.auto.value:auto-value-gson:0.4.5'
    compile 'commons-io:commons-io:2.5'
    compile 'org.apache.commons:commons-lang3:3.5'

    compile('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
        transitive = true
    }

    provided 'com.google.auto.value:auto-value:1.3'
    apt 'com.google.auto.value:auto-value:1.3'
    apt 'com.ryanharter.auto.value:auto-value-parcel:0.2.5'
    apt 'com.ryanharter.auto.value:auto-value-gson:0.4.5'
}

def askPassword() {
    return 'security -q find-generic-password -w -g -l farebot-release'.execute().text.trim()
}

gradle.taskGraph.whenReady { taskGraph ->
    if(taskGraph.hasTask(':assembleProdRelease')) {
        def password = askPassword()
        android.signingConfigs.release.storePassword = password
        android.signingConfigs.release.keyPassword = password
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    productFlavors {
        dev {
            minSdkVersion 21
            applicationId "com.codebutler.farebot.dev"
        }
        prod {
            minSdkVersion 15
        }
    }

    defaultConfig {
        targetSdkVersion 25
    }

    signingConfigs {
        debug {
            storeFile file('debug.keystore')
        }
        release {
            storeFile file('release.keystore')
            keyAlias 'ericbutler'
            storePassword ''
            keyPassword ''
        }
    }

    buildTypes {
        release {
            // runProguard true
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    lintOptions {
        abortOnError true
        disable 'MissingTranslation', 'InvalidPackage'
    }
}

checkstyle {
    ignoreFailures = false
    toolVersion = "6.17"
}
