buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.0-alpha2'
        classpath 'io.fabric.tools:gradle:1.21.6'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'checkstyle'

check.dependsOn 'checkstyle'

task checkstyle(type: Checkstyle) {
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    classpath = files()
}

repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    compile 'com.android.support:support-v4:23.4.0'
    compile 'com.google.android.gms:play-services-maps:9.0.1'
    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true
    }
    compile 'commons-io:commons-io:2.4'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile('org.simpleframework:simple-xml:2.7.1') {
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }
    compile project(':nfc-felica-lib')
    compile fileTree(dir: 'libs', include: '*.jar')
}

def password = null

def askPassword() {
    return 'security -q find-generic-password -g -l farebot-release'.execute().err.text.split('"')[1]
}

afterEvaluate {
    validateReleaseSigning.doFirst {
        password = askPassword()
        signingConfig.storePassword = password
        signingConfig.keyPassword = password
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    productFlavors {
        dev {
            minSdkVersion 21
            applicationId "com.codebutler.farebot.dev"
        }
        prod {
            minSdkVersion 15
        }
    }

    defaultConfig {
        targetSdkVersion 23
    }

    signingConfigs {
        debug {
            storeFile file('debug.keystore')
        }
        release {
            storeFile file('release.keystore')
            keyAlias 'ericbutler'
            storePassword ''
            keyPassword ''
        }
    }

    buildTypes {
        release {
            // runProguard true
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    lintOptions {
        abortOnError true
        disable 'MissingTranslation', 'InvalidPackage'
    }
}

checkstyle {
    ignoreFailures = false
    toolVersion = "6.11.2"
}
