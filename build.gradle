buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven { url 'https://maven.google.com' }
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.0-alpha5'
        classpath 'io.fabric.tools:gradle:1.22.1'
        classpath 'com.squareup.sqldelight:gradle-plugin:0.6.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.1.3"
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.14.0'
}

allprojects {
    repositories {
        mavenLocal()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
}

apply from: 'dependencies.gradle'

subprojects {
    apply plugin: 'checkstyle'

    afterEvaluate {project ->
        if (project.name.contains('farebot')) {
            check.dependsOn 'checkstyle'
            task checkstyle(type: Checkstyle) {
                configFile file('config/checkstyle/checkstyle.xml')
                source 'src'
                include '**/*.java'
                exclude '**/gen/**'
                exclude '**/IOUtils.java'
                exclude '**/Charsets.java'
                classpath = files()
            }
            checkstyle {
                ignoreFailures = false
                toolVersion = "6.17"
            }
        }
        if (project.hasProperty("android")) {
            android {
                compileSdkVersion vers.compileSdkVersion
                buildToolsVersion vers.buildToolsVersion

                defaultConfig {
                    minSdkVersion vers.minSdkVersion
                    targetSdkVersion vers.targetSdkVersion
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_7
                    targetCompatibility JavaVersion.VERSION_1_7
                }

                lintOptions {
                    abortOnError true
                    disable 'InvalidPackage','MissingTranslation'
                }

                dexOptions {
                    dexInProcess = true
                }
            }
        }
    }
}

configurations {
    ktlint
}

dependencies {
    ktlint 'com.github.shyiko:ktlint:0.8.3'
}

task lintKotlin(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "*/src/**/*.kt"
}
