/*
 * build.gradle
 *
 * This file is part of FareBot.
 * Learn more at: https://codebutler.github.io/farebot/
 *
 * Copyright (C) 2017 Eric Butler <eric@codebutler.com>
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

dependencies {
    implementation project(':farebot-base')
    implementation project(':farebot-app-persist')
    implementation project(':farebot-card')
    implementation project(':farebot-card-cepas')
    implementation project(':farebot-card-classic')
    implementation project(':farebot-card-desfire')
    implementation project(':farebot-card-desfire')
    implementation project(':farebot-card-felica')
    implementation project(':farebot-card-ultralight')
    implementation project(':farebot-transit')
    implementation project(':farebot-transit-bilhete')
    implementation project(':farebot-transit-clipper')
    implementation project(':farebot-transit-edy')
    implementation project(':farebot-transit-ezlink')
    implementation project(':farebot-transit-hsl')
    implementation project(':farebot-transit-manly')
    implementation project(':farebot-transit-myki')
    implementation project(':farebot-transit-octopus')
    implementation project(':farebot-transit-opal')
    implementation project(':farebot-transit-orca')
    implementation project(':farebot-transit-ovc')
    implementation project(':farebot-transit-seqgo')
    implementation project(':farebot-transit-stub')
    implementation project(':farebot-transit-suica')

    implementation libs.autoDispose
    implementation libs.autoDisposeAndroid
    implementation libs.supportDesign
    implementation libs.supportV4
    implementation libs.supportV7CardView
    implementation libs.supportV7RecyclerView
    implementation libs.playServicesMaps
    implementation libs.gson
    implementation libs.kotlinStdlib
    implementation libs.magellan
    implementation libs.rxBroadcast
    implementation libs.rxJava2
    implementation libs.rxRelay2

    implementation(libs.crashlytics) {
        transitive = true
    }

    implementation libs.dagger
    annotationProcessor libs.daggerCompiler
    kapt libs.daggerCompiler
}

static def askPassword() {
    return 'security -q find-generic-password -w -g -l farebot-release'.execute().text.trim()
}

gradle.taskGraph.whenReady { taskGraph ->
    if(taskGraph.hasTask(':farebot-app:packageRelease')) {
        def password = askPassword()
        android.signingConfigs.release.storePassword = password
        android.signingConfigs.release.keyPassword = password
    }
}

android {
    defaultConfig {
        versionCode 23
        versionName '3.0.0'
        multiDexEnabled true
    }

    signingConfigs {
        debug {
            storeFile file('../debug.keystore')
        }
        release {
            storeFile file('../release.keystore')
            keyAlias 'ericbutler'
            storePassword ''
            keyPassword ''
        }
    }

    buildTypes {
        debug {
            ext.enableCrashlytics = false
        }
        release {
            shrinkResources false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), '../config/proguard/proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}
